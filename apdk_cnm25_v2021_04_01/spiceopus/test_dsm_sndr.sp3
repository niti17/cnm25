test_dsm_sndr.sp3
* SQNDR curve test for dsm_arch_test.cir
* Requires cmload xtendedlib.cm

.control

delcirc all
destroy all
delete all
set color2=r000g000b000
set color3=r255g000b000
set color4=r000g255b000

setplot new
nameplot myvars
let aindBFS=(-80;-60;-40;-20;-15;-10;-6;-3;-2;-1.5;-1;-0.5;0;1.5;3)
let nsim=length(aindBFS)
let SQNDRdB=vector(nsim)*0

let k=0
while k le nsim-1

 source dsm_arch_test.cir
 let vfs=@@mi8:xi1[out_high]
 let vin=10^(aindBFS[k]/20)*vfs
 let @vi2[sin]=(0;vin;2e3)
 let out1max=@@mi2:xi1[out_max]
 let out1min=@@mi2:xi1[out_min]
 let out2max=@@mi5:xi1[out_max]
 let out2min=@@mi5:xi1[out_min]

 save v(vout)
 set reltol=1e-6
 set abstol=1e-15
 set vntol=1e-9
 ic v(vdac:xi1)=-2.0
 ic v(vint1out:xi1)=0.0
 ic v(vint2out:xi1)=0.0

 echo "Simulating for {round(aindBFS[k]*10)/10}dBFS ({k+1}/{nsim}) ..."
 tran 1e-9 4.1e-3
 
 shell "tran2psd -f 4.096e6 -w Blackman -z tran.raw > psd.raw"
 set filetype=ascii
 load "psd.raw"
 let p=(mag(v(vpsd))*myvars.vfs*2)^2

 let kbw=0
 cursor kbw right frequency 8e3
 let kleft=0
 cursor kleft right frequency 2e3
 let kright=kleft
 while ((p[kleft-1] lt p[kleft])&(kleft gt 0)) 
  kleft=kleft-1
  end
 while ((p[kright+1] lt p[kright])&(kright lt kbw)) 
  kright=kright+1
  end

 let S=sum(p[kleft,kright])
 let SQND=sum(p[3,kbw])
 let SQNDRdB=10*log(S/(SQND-S))
 let SQNDRb=((SQNDRdB-1.76)/6.02)

 let PdB=10*log(p)
 let AINdB=myvars.aindBFS[myvars.k]
 plot create plot1 xlog PdB vs frequency PdB[3,kbw] vs frequency[3,kbw] PdB[kleft,kright] vs frequency[kleft,kright]
 plot append plot1 xlog xlabel "Frequency [Hz]" ylabel "PSD [dBFS/bin]"
 plot append plot1 xlog title "SQNDR={round(SQNDRdB*100)/100}dB ({round(SQNDRb*100)/100}bit) for {round(AINdB*10)/10}dBFS@2kHz input"

 setplot myvars
 let SQNDRdB[k]=sp1.SQNDRdB

 destroy sp1
 destroy tran1
 delcirc
 let k=k+1

 end

set filetype=ascii
unset appendwrite
write "test_dsm_sndr.raw" aindBFS SQNDRdB

plot create plot2 SQNDRdB vs aindBFS
plot append plot2 xlabel "Input amplitude [dBFS]" ylabel "SQNDR [dB]"
plot append plot2 title "Dynamic range for {out1min}<zint1out<{out1max} and {out2min}<zint2out<{out2max}"
plot append plot2 xlimit -100 5 ylimit 0 100
plot print plot2 file test_dsm_sndr.pdf

.endc

.end

