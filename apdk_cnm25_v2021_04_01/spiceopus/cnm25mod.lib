
*** CNM25 device models

*** Common part

.lib common

.subckt cnm25modn d g s b param: w=3u l=3u ad=0 as=0 pd=0 ps=0 m=1
.param vth0eff = vth0n+rndgauss(avth0/sqrt(m*w*l))
.param u0eff = u0n*(1+rndgauss(rau0/sqrt(m*w*l)))
.model nbsim nmos
+ LEVEL   = 53
+ VERSION = 3.2.4          TNOM    = 27             TOX     = 3.75E-8
+ XJ      = 1.5E-7         NCH     = 1.7E17         VTH0	= {vth0eff}
+ K1      = 1.17296        K2      = -0.05          K3      = 11.2079
+ K3B     = -1.59332       W0      = 1.00727E-6     NLX     = -1E-9
+ DVT0W   = 0              DVT1W   = 0              DVT2W   = -0.032
+ DVT0    = 4.11104        DVT1    = 0.366189       DVT2    = -0.182099
+ U0      = {u0eff}        UA      = 1.72783E-10    UB      = 5E-18
+ UC      = 4.01727E-11    VSAT    = 1.848E5        A0      = 1.05122
+ AGS     = 0.111468       B0      = 1.6771E-7      B1      = -5.04982E-9
+ KETA    = -0.047         A1      = 0              A2      = 1
+ RDSW    = 3.65E3         PRWG    = 0.0338512      PRWB    = -1E-3
+ WR      = 1              WINT    = 4.55906E-7     LINT    = 9E-7
+ XL      = 0              XW      = 0              DWG     = -2.5492E-8
+ DWB     = 3.22958E-8     VOFF    = -0.124454      NFACTOR = 1.04789
+ CIT     = 0              CDSC    = 2.4E-4         CDSCD   = 0
+ CDSCB   = 0              ETA0    = 0.0354838      ETAB    = -0.07
+ DSUB    = 0.56           PCLM    = 1.96809        PDIBLC1 = 0.482853
+ PDIBLC2 = 0.01           PDIBLCB = 0              DROUT   = 0.415163
+ PSCBE1  = 5.99202E8      PSCBE2  = 5E-5           PVAG    = 0.0141775
+ DELTA   = 3.6636E-3      MOBMOD  = 1              PRT     = 0
+ UTE     = -1.5           KT1     = 0              KT1L    = 0
+ KT2     = 0              UA1     = 4.31E-9        UB1     = -7.61E-18
+ UC1     = -5.6E-11       AT      = 3.3E4          NQSMOD  = 0
+ WL      = 0              WLN     = 1              WW      = 0
+ WWN     = 1              WWL     = 0              LL      = 0
+ LLN     = 1              LW      = 0              LWN     = 1
+ LWL     = 0              CAPMOD  = 2              CJ      = 2.940466E-4
+ PB      = 0.6681951      MJ      = 0.438766       CJSW    = 5.450602E-10
+ PBSW    = 0.4            MJSW    = 0.2725869      TCJ     = 0
+ TPB     = 0              TCJSW   = 0              TPBSW   = 0
+ NOFF    = 1              ACDE    = 1              MOIN    = 15
+ TPBSWG  = 0              TCJSWG  = 0              PRDSW   = -3.85642E3
+ PVSAT   = -1.8E5         CGDO    = 9.89535E-10    CGSO    = 9.89535E-10
+ NOIMOD  = 1              AF      = 1.33           KF      = 1e-29 
mn d g s b nbsim w={w} l={l} ad={ad} as={as} pd={pd} ps={ps} m={m}
.ends

.subckt cnm25modp d g s b param: w=3u l=3u ad=0 as=0 pd=0 ps=0 m=1
.param vth0eff = vth0p+rndgauss(avth0/sqrt(m*w*l))
.param u0eff = u0p*(1+rndgauss(rau0/sqrt(m*w*l)))
.model pbsim pmos
+ LEVEL   = 53
+ VERSION = 3.2.4          TNOM    = 27             TOX     = 3.75E-8
+ XJ      = 1.5E-7         NCH     = 1.7E17         VTH0    = {vth0eff}
+ K1      = 0.74278        K2      = -4.93305E-5    K3      = -77.5174
+ K3B     = -3.17908       W0      = 6.70948E-6     NLX     = 1.44524E-7
+ DVT0W   = 0              DVT1W   = 0              DVT2W   = -0.032
+ DVT0    = 1.61621        DVT1    = 0.15752        DVT2    = -0.05
+ U0      = {u0eff}        UA      = 2.65041E-9     UB      = 4.97595E-18
+ UC      = -9.99573E-11   VSAT    = 5E5            A0      = 0.804733
+ AGS     = 0.0783374      B0      = 3.55811E-7     B1      = 2.01182E-10
+ KETA    = -0.047         A1      = 0              A2      = 1
+ RDSW    = 5.41703E3      PRWG    = 0.013649       PRWB    = -1E-3
+ WR      = 1              WINT    = 5E-7           LINT    = 8E-7
+ XL      = 0              XW      = 0              DWG     = -1.44072E-8
+ DWB     = 5.72498E-8     VOFF    = -0.196491      NFACTOR = 0.924527
+ CIT     = 0              CDSC    = 2.4E-4         CDSCD   = 0
+ CDSCB   = 0              ETA0    = 0.3989455      ETAB    = -0.07
+ DSUB    = 0.56           PCLM    = 4.3768578      PDIBLC1 = 0.7281865
+ PDIBLC2 = 0.0140758      PDIBLCB = 0              DROUT   = 0.2398601
+ PSCBE1  = 8E8            PSCBE2  = 5E-5           PVAG    = 0.0099941
+ DELTA   = 0.0634845      MOBMOD  = 1              PRT     = 0
+ UTE     = -1.5           KT1     = 0              KT1L    = 0
+ KT2     = 0              UA1     = 4.31E-9        UB1     = -7.61E-18
+ UC1     = -5.6E-11       AT      = 3.3E4          NQSMOD  = 0
+ WL      = 0              WLN     = 1              WW      = 0
+ WWN     = 1              WWL     = 0              LL      = 0
+ LLN     = 1              LW      = 0              LWN     = 1
+ LWL     = 0              CAPMOD  = 2              CJ      = 3.728047E-4
+ PB      = 0.7982792      MJ      = 0.4562281      CJSW    = 3.946756E-10
+ PBSW    = 0.587129       MJSW    = 0.2658605      TCJ     = 0
+ TPB     = 0              TCJSW   = 0              TPBSW   = 0
+ NOFF    = 1              ACDE    = 1              MOIN    = 15
+ TPB     = 0              TPBSW   = 0              TPBSWG  = 0
+ TCJ     = 0              TCJSW   = 0              TCJSWG  = 0
+ CGDO    = 1.2894E-9      CGSO    = 1.2894E-9
+ NOIMOD  = 1              AF      = 1.33           KF      = 1e-29
mp d g s b pbsim w={w} l={l} ad={ad} as={as} pd={pd} ps={ps} m={m}
.ends

.subckt cnm25cpoly t b param: w=30u l=30u m=1
.param cjeff = cj*(1+rndgauss(racj/sqrt(m*w*l)))
.model cap c CJ = {cjeff}  CJSW = 0.0
ci t b cap w={w} l={l} m={m}
.ends

.endl common

*** Process corners

.lib ttt
.param vth0n = 0.860363
.param vth0p = -1.52069
.param u0n = 0.0573986
.param u0p = 0.0228166
.param cj = 4.227E-4
.param avth0 = 0
.param rau0 = 0
.param racj = 0
.lib 'cnm25mod.lib' common
.endl ttt

.lib sss
.param vth0n = 1.00467
.param vth0p = -1.73564
.param u0n = 0.0367598
.param u0p = 0.0140327
.param cj = 4.650E-4
.param avth0 = 0
.param rau0 = 0
.param racj = 0
.lib 'cnm25mod.lib' common
.endl sss

.lib fff
.param vth0n = 0.63934
.param vth0p = -1.20160
.param u0n = 0.0780374
.param u0p = 0.0316005
.param cj = 3.804E-4
.param avth0 = 0
.param rau0 = 0
.param racj = 0
.lib 'cnm25mod.lib' common
.endl fff

*** Montecarlo mismatching
 AVto{n,p} = 30mVum
 AUo{n,p}/Uo = 5%um
 ACj/Cj = 0.5%um

.lib ttt_mc
.param vth0n = 0.860363
.param vth0p = -1.52069
.param u0n = 0.0573986
.param u0p = 0.0228166
.param cj = 4.227E-4
.param avth0 = 30e-9
.param rau0 = 5e-8
.param racj = 5e-9
.lib 'cnm25mod.lib' common
.endl ttt_mc

*** end of file

